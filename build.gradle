plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

// Project Configuration

final String projectGroup = 'com.rayferric'
final String projectVersion = '1.0.0'
final String mainClass = 'com.rayferric.spatialwalker.Main'
final JavaVersion javaVersion = JavaVersion.VERSION_14
final String gradleVersion = '6.6'

// Apply Configuration

group projectGroup
version projectVersion

shadowJar {
    manifest {
        attributes 'Main-Class': mainClass
    }
}

compileJava.sourceCompatibility = javaVersion

wrapper.gradleVersion = gradleVersion
wrapper.distributionType = Wrapper.DistributionType.ALL

// Dependencies

repositories {
    mavenCentral()
    maven {
        url 'https://oss.sonatype.org/content/repositories/snapshots/'
    }
}

dependencies {
    implementation platform('org.lwjgl:lwjgl-bom:3.2.4-SNAPSHOT')

    implementation 'org.lwjgl:lwjgl'
    implementation 'org.lwjgl:lwjgl-assimp'
    implementation 'org.lwjgl:lwjgl-glfw'
    implementation 'org.lwjgl:lwjgl-openal'
    implementation 'org.lwjgl:lwjgl-opengl'
    implementation 'org.lwjgl:lwjgl-stb'

    runtimeOnly 'org.lwjgl:lwjgl::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-assimp::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-glfw::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-openal::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-opengl::natives-windows'
    runtimeOnly 'org.lwjgl:lwjgl-stb::natives-windows'

    implementation group: 'cz.advel.jbullet', name: 'jbullet', version: '20101010-1'
}

// Tasks

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    into 'lib'
}

task cleanDependencies(type: Delete) {
    delete 'lib'
}